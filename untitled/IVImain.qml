import QtQuick 2.15
import QtQuick.Controls 2.15
import QtQuick.Window 2.15

import QtQuick.Layouts 1.15
import "." 1.0

Window {
    id: root
    width: 500
    height: 700
    x: 900
    visible: true
    title: "IVI HMI"

    property string currentTheme: "dark" // or "light"

    // --- NgÃ´n ngá»¯ hiá»‡n táº¡i ---
    property string currentLanguage: "vi"  // default

    property int unreadMessageCount: 0

    // --- Dictionary báº£n dá»‹ch ---
    property var dict: {
        "vi": {
            "CÃ i Ä‘áº·t": "CÃ i Ä‘áº·t",
            "NgÃ´n ngá»¯": "NgÃ´n ngá»¯",
            "Cuá»™c gá»i": "Cuá»™c gá»i",
            "Tin nháº¯n": "Tin nháº¯n",
            "Ã‚m nháº¡c": "Ã‚m nháº¡c",
            "Báº£n Ä‘á»“": "Báº£n Ä‘á»“",
            "Thá»i tiáº¿t": "Thá»i tiáº¿t"
        },
        "en": {
            "CÃ i Ä‘áº·t": "Settings",
            "NgÃ´n ngá»¯": "Language",
            "Cuá»™c gá»i": "Calls",
            "Tin nháº¯n": "Messages",
            "Ã‚m nháº¡c": "Music",
            "Báº£n Ä‘á»“": "Maps",
            "Thá»i tiáº¿t": "Weather"
        }
    }

    // --- HÃ m dá»‹ch ---
    function tr(key) {
        if(dict[currentLanguage] && dict[currentLanguage][key])
            return dict[currentLanguage][key]
        return key
    }
    function loadLanguage(lang) {
        currentLanguage = lang
        console.log("Language changed to:", lang)
    }

    // --- MÃ n hÃ¬nh khá»Ÿi Ä‘á»™ng ---
    Rectangle {
        id: splashScreen
        anchors.fill: parent
        visible: true
        color: "#000000"

        // Ná»n gradient Ä‘áº¹p
        Rectangle {
            anchors.fill: parent
            gradient: Gradient {
                GradientStop { position: 0.0; color: "#0A0A0A" }
                GradientStop { position: 1.0; color: "#1A1A1A" }
            }
        }

        // Váº§ng sÃ¡ng lan rá»™ng (glow)
        Rectangle {
            id: glow
            width: 300
            height: 300
            radius: 200
            color: "#00FFAA"
            opacity: 0.0
            anchors.centerIn: parent

            SequentialAnimation {
                running: true

                // Lan sÃ¡ng nháº¹
                NumberAnimation { target: glow; property: "opacity"; from: 0.0; to: 0.25; duration: 2500; easing.type: Easing.InOutQuad }
                NumberAnimation { target: glow; property: "opacity"; from: 0.25; to: 0.0; duration: 2500; easing.type: Easing.InOutQuad }
            }
        }

        // Logo zoom-in + fade-in + fade-out
        Image {
            id: logo
            source: "qrc:/imgIVI/car.svg"
            anchors.centerIn: parent
            width: 130
            height: 130
            opacity: 0.0
            scale: 0.7
            smooth: true
            antialiasing: true

            SequentialAnimation {
                id: logoAnim

                // Zoom in + fade in
                ParallelAnimation {
                    NumberAnimation { target: logo; property: "opacity"; from: 0.0; to: 1.0; duration: 2500; easing.type: Easing.OutCubic }
                    NumberAnimation { target: logo; property: "scale"; from: 0.7; to: 1.0; duration: 2500; easing.type: Easing.OutBack }
                }

                PauseAnimation { duration: 800 }

                // Fade out + zoom out nháº¹
                ParallelAnimation {
                    NumberAnimation { target: logo; property: "opacity"; from: 1.0; to: 0.0; duration: 2000; easing.type: Easing.InCubic }
                    NumberAnimation { target: logo; property: "scale"; from: 1.0; to: 1.2; duration: 2000; easing.type: Easing.InOutQuad }
                }

                onStopped: {
                    splashScreen.visible = false
                    mainScreen.visible = true
                }
            }

            Component.onCompleted: logoAnim.start()
        }
    }

    // --- MÃ n hÃ¬nh chÃ­nh (áº©n ban Ä‘áº§u) ---
    Rectangle {
        id: mainScreen
        anchors.fill: parent
        visible: false

        Rectangle {
            id: background
            anchors.fill: parent
            gradient: Gradient {
                GradientStop { position: 0.0; color: root.currentTheme === "dark" ? Theme.bgDark : Theme.bgLight }
                GradientStop { position: 1.0; color: root.currentTheme === "dark" ? Theme.bgDark : Theme.bgLight }
            }
            Behavior on gradient { ColorAnimation { duration: 500 } }
        }

        // Ãnh sÃ¡ng neon má» trung tÃ¢m
        Rectangle {
            id: neonGlow
            width: parent.width * 0.9
            height: parent.height * 0.9
            anchors.centerIn: parent
            radius: width / 2
            color: Theme.neon   // neon xanh lÃ¡
            opacity: 0.08       // ráº¥t nháº¹, khÃ´ng gÃ¢y chÃ³i
        }

        GridLayout {
            id: appGrid
            anchors.centerIn: parent
            anchors.leftMargin: 30
            anchors.rightMargin: 30
            columns: 3           // ğŸ”¹ 3 cá»™t
            rows: 3              // ğŸ”¹ 3 hÃ ng
            columnSpacing: 60
            rowSpacing: 30

            Repeater {
                model: appModel

                Item {
                    Layout.preferredWidth: 100
                    Layout.preferredHeight: 100

                    Rectangle {
                        id: iconContainer
                        width: 70
                        height: 70
                        radius: 30
                        anchors.horizontalCenter: parent.horizontalCenter
                        anchors.top: parent.top
                        anchors.topMargin: 10
                        color: "transparent"

                        property bool pressed: false

                        border.color: pressed
                                      ? Theme.textDark
                                      : (root.currentTheme === "dark" ? Theme.borderDark : Theme.borderLight)
                        border.width: 3

                        Behavior on border.color { ColorAnimation { duration: 300 } } // hiá»‡u á»©ng mÆ°á»£t

                        Image {
                            id: iconSetting
                            anchors.centerIn: parent
                            width: parent.width / 1.2
                            height: parent.height /1.2
                            z: -1
                            fillMode: Image.PreserveAspectFit
                            source: root.currentTheme === "dark" ? iconD : iconL

                            opacity: 1.0

                            Behavior on opacity {
                                NumberAnimation { duration: 200; easing.type: Easing.InOutQuad }
                            }
                        }

                        Rectangle {
                            id: messageBadge
                            visible: name === "Tin nháº¯n" && root.unreadMessageCount > 0
                            width: 24
                            height: 24
                            radius: 12
                            color: "red"
                            border.color: "white"
                            border.width: 2
                            anchors.top: iconContainer.top
                            anchors.right: iconContainer.right
                            anchors.margins: -5

                            Text {
                                anchors.centerIn: parent
                                text: root.unreadMessageCount
                                color: "white"
                                font.bold: true
                                font.pixelSize: 14
                            }
                        }

                        MouseArea {
                            anchors.fill: parent
                            onPressed: iconContainer.pressed = true
                            onReleased: iconContainer.pressed = false
                            onClicked: {
                                console.log("Clicked on " + root.tr(name))
                                switch(root.tr(name)) {
                                case "CÃ i Ä‘áº·t":
                                case "Settings":
                                    settingsWin.visible = true
                                    break
                                case "NgÃ´n ngá»¯":
                                case "Language":
                                    languageDialog.open()
                                    break
                                case "Cuá»™c gá»i":
                                case "Calls":
                                    incomingCall.visible = true
                                    break
                                case "Tin nháº¯n":
                                case "Messages":
                                    unreadMessageCount = 0      // reset badge khi má»Ÿ
                                    messagePage.visible = true  // má»Ÿ mÃ n hÃ¬nh tin nháº¯n
                                    break
                                case "Thá»i tiáº¿t":
                                case "Weather":
                                    root.unreadMessageCount++
                                    messageModel.append({
                                        sender: "ESP32",
                                        content: "Tin nháº¯n test " + root.unreadMessageCount
                                    })
                                    break
                                }
                            }
                        }
                    }

                    Text {
                        text: root.tr(name)
                        color: root.currentTheme === "dark" ? Theme.textDark : Theme.textLight
                        font.pixelSize: 18
                        horizontalAlignment: Text.AlignHCenter
                        anchors.top: iconContainer.bottom
                        anchors.horizontalCenter: parent.horizontalCenter
                        anchors.topMargin: 8
                    }
                }
            }
        }

        // --- Model dá»¯ liá»‡u á»©ng dá»¥ng ---
        ListModel {
            id: appModel
            ListElement { name: "CÃ i Ä‘áº·t"; iconD: "qrc:/imgIVI/setting.svg"; iconL: "qrc:/imgIVI/settingL.svg" }
            ListElement { name: "NgÃ´n ngá»¯"; iconD: "qrc:/imgIVI/language_D1.svg"; iconL: "qrc:/imgIVI/language-L.svg" }
            ListElement { name: "Cuá»™c gá»i"; iconD: "qrc:/imgIVI/call_D.svg"; iconL: "qrc:/imgIVI/call_L.svg" }
            ListElement { name: "Tin nháº¯n"; iconD: "qrc:/imgIVI/message_D.svg"; iconL: "qrc:/imgIVI/message_L.svg" }
            ListElement { name: "Ã‚m nháº¡c"; iconD: "qrc:/imgIVI/music_D.svg"; iconL: "qrc:/imgIVI/music_L.svg" }
            ListElement { name: "Báº£n Ä‘á»“"; iconD: "qrc:/imgIVI/map_D.svg"; iconL: "qrc:/imgIVI/map_L.svg" }
            ListElement { name: "Thá»i tiáº¿t"; iconD: "qrc:/imgIVI/weather_D.svg"; iconL: "qrc:/imgIVI/weather_L.svg" }
        }

        // --- Model tin nháº¯n ---
        ListModel {
            id: messageModel
        }

        // --- Gá»i cá»­a sá»• CÃ i Ä‘áº·t ---
        SettingWindow {
            id: settingsWin
            onThemeChanged: {
                root.currentTheme = newTheme
                console.log("Theme changed to:", newTheme)
            }
        }

        LanguageWindow {
            id: languageDialog
            onLanguageSelected: (lang) => loadLanguage(lang)
        }

        IncomingCallScreen {
            id: incomingCall
            visible: false

            onAcceptCall: {
                visible = false
                activeCall.startCall(callerName)
            }
            onRejectCall: {
                visible = false
                mainScreen.visible = true
            }
        }

        ActiveCallScreen {
            id: activeCall
            visible: false
            onEndCall: {
                visible = false
                mainScreen.visible = true
            }
        }

        // Káº¾T Ná»I Vá»šI SerialHandler tá»« C++
        Connections {
            target: serialHandler
            //cuoc goi den
            function onIncomingCall(callerName) {
                console.log("ğŸ“ Cuá»™c gá»i Ä‘áº¿n tá»«:", callerName)
                incomingCall.callerName = callerName
                incomingCall.visible = true
            }
            function onEndCall() {
                console.log("ğŸ“´ Káº¿t thÃºc cuá»™c gá»i tá»« ESP32")
                incomingCall.visible = false
                activeCall.visible = false
                mainScreen.visible = true
            }
            //tin nhan den
            function onMessageReceived(sender, content) {
                console.log("ğŸ“¨ Tin nháº¯n Ä‘áº¿n tá»« ESP32:", content)

                root.unreadMessageCount++

                messageModel.append({
                    sender: sender,
                    content: content
                })
            }
        }

        MessagePage {
            id: messagePage
            anchors.fill: parent
            visible: false
            messageModel: messageModel

            onBackRequested: {
                messagePage.visible = false
            }
        }
    }
}
